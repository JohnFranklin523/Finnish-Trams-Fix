#define VEH mlrvi
/*[[MAKE
build/mlrvi_1.png src/gfx/xcf/vehs/mlrvi.xcf bg 1 panto;
build/mlrvi_1_open.png src/gfx/xcf/vehs/mlrvi.xcf bg 1 doors_1 panto;
build/mlrvi_2_4.png src/gfx/xcf/vehs/mlrvi.xcf bg 2_4;
build/mlrvi_2_4_open.png src/gfx/xcf/vehs/mlrvi.xcf bg 2_4 doors_2_4;
build/mlrvi_3.png src/gfx/xcf/vehs/mlrvi.xcf bg 3;
build/mlrvi_5.png src/gfx/xcf/vehs/mlrvi.xcf bg 5;
build/mlrvi_5_open.png src/gfx/xcf/vehs/mlrvi.xcf bg 5 doors_5;
]]*/

SPR_PUR(ID(spr_pur), "build/mlrvi_1.png")
SPR(ID(spr_1), "build/mlrvi_1.png", 4)
SPR(ID(spr_1_open), "build/mlrvi_1_open.png", 4)
SPR(ID(spr_2_4), "build/mlrvi_2_4.png", 2)
SPR(ID(spr_2_4_open), "build/mlrvi_2_4_open.png", 2)
SPR(ID(spr_3), "build/mlrvi_3.png", 2)
spriteset(mlrvi_spr_5, "build/mlrvi_5.png") { tmpl_mlrvi() } // Temporarily set to avoid sprite misalignments, length: 4
spriteset(mlrvi_spr_5_open, "build/mlrvi_5_open.png") { tmpl_mlrvi() } // Temporarily set to avoid sprite misalignments, length: 4
spritegroup ID(sprg_1) {
    loading: ID(spr_1_open);
    loaded: ID(spr_1);
}
spritegroup ID(sprg_2_4) {
    loading: ID(spr_2_4_open);
    loaded: ID(spr_2_4);
}
spritegroup ID(sprg_5) {
    loading: ID(spr_5_open);
    loaded: ID(spr_5);
}

SWITCH(ID(sw_arti), extra_callback_info1,
    1..4: VEH;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
)
SWITCH_SUBTYPE(ID(sw_cst),
    string(STR_SUBTYPE_REAL,
        string(STR_HKL), string(STR_LIVERY_YELLOW_PALE_GREEN)
    )
)
SWITCH_RECOLOUR(ID(sw_cm),
    recolour_vario
)
SWITCH(ID(sw_gfx), position_in_consist,
    0: ID(sprg_1);
    2: ID(spr_3);
    4: ID(sprg_5);
    default: ID(sprg_2_4);
)
SWITCH(ID(sw_len), position_in_consist,
    1..3: 2;
    default: 4;
)

PROPS_COMMON(VEH)
PROPS(VEH,
    cargo_capacity: 17;
    cost_factor: 90;
    introduction_date: date(1998,01,01);
    length: 4;
    loading_speed: 5;
    name: string(STR_NAME_MLRVI);
    model_life: 40;
    retire_early: 25;
    power: 12*45 kW;
    running_cost_factor: 90;
    speed: 65 km/h;
    weight: 46 ton;
)
GFX(VEH,
    additional_text: MORE_TEXT("2", string(STR_D_MLRVI),
                               string(STR_TRIVIA_MLRVI));
    articulated_part: ID(sw_arti);
    cargo_subtype_text: ID(sw_cst);
    colour_mapping: ID(sw_cm);
    default: ID(sw_gfx);
    length: ID(sw_len);
    purchase: ID(spr_pur);
)

#undef VEH
